cstr ban(vs <: vms, ns <: nodes) ::=
    """ {"id":"ban", "vms":@vs, "nodes":@ns} """
    !(v : vs) vmState(v) = running --> host(v) /: ns

cstr cumulatedRunningCapacity(ns <: nodes, nb : int) ::=
        """{"id":"cumulatedRunningCapacity","nodes":@ns,"continuous":false,"amount":@nb} """
        sum({card(running(n)). n : ns}) <= nb

cstr fence(vs <: vms, ns <: nodes) ::=
    """ {"id":"fence", "vms":@vs, "nodes":@ns} """
    !(v : vs) vmState(v) = running --> host(v) : ns

cstr gather(vs <: vms) ::=
    """ {"id":"gather",  "vms":@vs, "continuous": false} """
    !(x,y : vs)  (x /= y & vmState(x) = running & vmState(y) = running) --> host(x) = host(y)

cstr lonely (vs <: vms) ::=
    """ {"id":"lonely", "vms":@vs, "continuous" : false} """
	!(i : vs) vmState(i) = running --> (colocated(i) - {i}) /<: vs

cstr offline(ns <: nodes) ::=
    """ {"id":"offline",  "nodes":@n} """
    !(n : ns) nodeState(n) = offline --> card(hosted(n)) = 0

cstr online(ns <: nodes) ::=
    """ {"id": "online", "nodes": @n} """
    !(n : ns) nodeState(n) = online

cstr running(vs <: vms) ::=
    """ {"id": "running", "vms": @vs} """
    !(v : vs) vmState(v) = running --> (host(v) : nodes & nodeState(host(v)) = online)

cstr ready(vs <: vms) ::=
    """ {"id": "ready", "vms": @vs} """
    !(v : vs) vmState(v) = ready --> host(v) /: nodes

cstr sleeping(vs <: vms) ::= 
    """ {"id": "sleeping", "vms": @vs} """
    !(v : vs) vmState(v) = sleeping --> (host(v) : nodes & nodeState(host(v)) = online)

cstr terminated(vs <: vms) ::=
    """ {"id": "terminated", "vms": @vs} """
    !(v : vs) vmState(v) = terminated --> host(v) /: nodes

cstr root (vs <: vms) ::=
    """ {"id":"root", "vms":@vs} """
    !(v : vs) vmState(v) = running --> $host(v) = host(v)

cstr singleRunningCapacity(ns <: nodes, nb : int) ::=
    """{"id":"singleRunningCapacity","nodes":@ns,"continuous":false,"amount":@nb} """
	!(n : ns) card(running(n)) <= nb

cstr spread(vs <: vms) ::=
    """ {"id":"spread",  "vms":@vs, "continuous": false} """
     !(x,y : vs) (x /= y &  vmState(x) = running & vmState(y) = running) --> host(x) /= host(y)

cstr quarantine(ns <: nodes) ::=
    """ {"id":"quarantine",  "ns":@ns, "continuous": false} """
     !(n : ns) !(v : hosted(n)) root(v) & ban({hosted(n2). n2 /: ns})

cstr preserve(vs <: vms, id : string, qty: int) ::=
    """ {"id": "preserve", "vms": @vs, "rc":@id, "amount":@qty} """
    !(v : vs) vmState(v) = running --> cons(v, id) <= qty

cstr singleResourcecapacity(ns <: nodes, id : string, qty: int) ::=
    """ {"id": "singleResourceCapacity", "nodes": @ns, "rc":@id, "amount":@qty} """
    !(n : ns) nodeState(n) = online --> capa(n, id) <= qty

cstr cumulatedResourcecapacity(ns <: nodes, id : string, qty: int) ::=
    """ {"id": "cumulatedResourceCapacity", "nodes": @ns, "rc":@id, "amount":@qty} """
    sum({capa(i, id). i : ns}) <= qty

cstr overbook(ns <: nodes, id : string, qty: real) ::=
    """ {"id": "overbook", "nodes": @ns, "rc":@id, "amount":@qty} """
    !(n : ns) sum({cons(v, id). v : running(n)}) * qty < capa(n, id)

cstr maxOnline(ns <: nodes, nb : int) ::=
    """ {"id":"maxOnline","nodes":@ns,"amount":@nb,"continuous":false} """
   	card({i. i : ns & nodeState(i) = online}) <= nb

cstr among (vs <: vms, groups : P(nodes)) ::=
    """ {"id":"among", "nodes":@ns, "continuous" : false} """
    ?(g : groups) {host(i). i : vs & vmState(i) = running} <: g

cstr split(part : P(vms)) ::=
    """ {"id":"split", "vms":@vs, "continuous" : true} """
    { {host(v). v : p & vmState(v) = running}. p : part} : P(nodes)

cstr splitAmong(vs : P(vms), part : P(nodes)) ::=
    """ {"id":"splitAmong", "vms":@vs, "nodes":@ns, "continuous" : true} """
    !(p : part) among(vs, p) & split(vs)